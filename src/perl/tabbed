#! perl

sub refresh {
   my ($self) = @_;

   my $ncol = $self->ncol;

   my $text = " " x $ncol;
   my $rend = [(urxvt::DEFAULT_RSTYLE | urxvt::RS_RVid) x $ncol];

   my @ofs;

   substr $text, 0, 7, "[NEW] |";
   @$rend[0 .. 5] = (urxvt::OVERLAY_RSTYLE) x 6;
   push @ofs, [0, 6, sub { $_[0]->new_tab }];

   my $ofs = 7;
   my $idx = 0;

   for my $tab (@{ $self->{tabs} }) {
      $idx++;

      my $txt = " $idx ";
      my $len = length $txt;

      substr $text, $ofs, $len + 1, "$txt|";
      @$rend[$ofs .. $ofs + $len - 1] = (urxvt::OVERLAY_RSTYLE) x $len
         if $tab == $self->{cur};

      push @ofs, [ $ofs, $ofs + $len, sub { $_[0]->make_current ($tab) } ];

      $ofs += $len + 1;
   }

   $self->{tabofs} = \@ofs;

   $self->ROW_t (0, $text, 0, 0, $ncol);
   $self->ROW_r (0, $rend, 0, 0, $ncol);

   $self->want_refresh;
}

sub new_tab {
   my ($self) = @_;

   my $offset = $self->fheight;

   # save a backlink to us, make sure tabbed is inactive
   push @urxvt::TERM_INIT, sub {
      my ($term) = @_;
      $term->{parent} = $self;

      $term->resource ($_->[0] => $_->[1])
         for @{ $self->{resource} || [] };

      $term->resource (perl_ext_2 => $term->resource ("perl_ext_2") . ",-tabbed");
      
   };

   push @urxvt::TERM_EXT, urxvt::ext::tabbed::tab::;

   my $term = new urxvt::term
      $self->env, $urxvt::RXVTNAME,
      -embed => $self->parent,
   ;
}

sub configure {
   my ($self) = @_;

   my $tab = $self->{cur};

   $tab->XMoveResizeWindow (
      $tab->parent,
      0, $self->{tabheight},
      $self->width, $self->height - $self->{tabheight}
   );

   my $wm_normal_hints = $self->XInternAtom ("WM_NORMAL_HINTS");

   for my $atom ($tab->XListProperties ($tab->parent)) {
      my ($type, $format, $items) = $self->XGetWindowProperty ($tab->parent, $atom);

      if ($atom == $wm_normal_hints) {
         my (@hints) = unpack "l!*", $items;

         $hints[$_] += $self->{tabheight} for (4, 6, 16);

         $items = pack "l!*", @hints;
      }
      $self->XChangeWindowProperty ($self->parent, $atom, $type, $format, $items);
   }

   $self->refresh;
}

sub make_current {
   my ($self, $tab) = @_;

   if (my $cur = $self->{cur}) {
      $cur->XUnmapWindow ($cur->parent) if $cur->mapped;
      $cur->focus_out;
   }
   
   $self->{cur} = $tab;

   $self->configure;
   $tab->focus_in;
   $tab->XMapWindow ($tab->parent);
   $self->refresh;

   ()
}

sub on_button_press {
   1
}

sub on_button_release {
   my ($self, $event) = @_;

   if ($event->{row} == 0) {
      for my $button (@{ $self->{tabofs} }) {
         $button->[2]->($self, $event)
            if $event->{col} >= $button->[0]
               && $event->{col} < $button->[1];
      }
   }

   1
}

sub on_motion_notify {
   1
}

sub on_init {
   my ($self) = @_;

   for (qw(name perl_ext_1 perl_ext_2)) {
      my $val = $self->resource ($_);

      push @{ $self->{resource} }, [$_ => $val]
        if defined $val;
   }

   $self->resource (int_bwidth => 0);
   $self->resource (name => "URxvt.tab");
   $self->resource (pty_fd => -1);

   $self->option ($urxvt::OPTION{scrollBar}, 0);

   ()
}

sub on_start {
   my ($self) = @_;

   $self->{tabheight} = $self->int_bwidth + $self->fheight + $self->lineSpace;

   $self->new_tab;

   ()
}

sub on_configure_notify {
   my ($self, $event) = @_;

   $self->configure;

   ()
}

sub on_wm_delete_window {
   my ($self) = @_;

   $_->destroy for @{ $self->{tabs} };

   1
}

sub tab_start {
   my ($self, $tab) = @_;

   push @{ $self->{tabs} }, $tab;

#   $tab->{name} ||= scalar @{ $self->{tabs} };
   $self->make_current ($tab);

   ()
}

sub tab_destroy {
   my ($self, $tab) = @_;

   $self->{tabs} = [ grep $_ != $tab, @{ $self->{tabs} } ];

   if (@{ $self->{tabs} }) {
      if ($self->{cur} == $tab) {
         delete $self->{cur};
         $self->make_current ($self->{tabs}[-1]);
      } else {
         $self->refresh;
      }
   } else {
      # delay destruction a tiny bit
      $self->{destroy} = urxvt::iw->new->start->cb (sub { $self->destroy });
   }

   ()
}

sub tab_key_press {
   my ($self, $tab, $event, $keysym, $str) = @_;

   if ($event->{state} & urxvt::ShiftMask
       && ($keysym == 0xff51 || $keysym == 0xff53)) {
      my ($idx) = grep $self->{tabs}[$_] == $tab, 0 .. $#{ $self->{tabs} };

      --$idx if $keysym == 0xff51;
      ++$idx if $keysym == 0xff53;

      $self->make_current ($self->{tabs}[$idx % @{ $self->{tabs}}]);
      $self->refresh;
      
      return 1;
   }

   ()
}

package urxvt::ext::tabbed::tab;

# helper extension implementing the subwindows of a tabbed terminal.
# simply proxies all interesting calls back to the tabbed class.

{
   for my $hook qw(start destroy key_press) {
      eval qq{
         sub on_$hook {
            my \$parent = \$_[0]{term}{parent}
               or return;
            \$parent->tab_$hook (\@_)
         }
      };
      die if $@;
   }
}



